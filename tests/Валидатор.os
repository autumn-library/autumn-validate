#Использовать autumn
#Использовать ".."
#Использовать "."
#Использовать 1testrunner
#Использовать asserts

Процедура ПередЗапускомТеста() Экспорт
	Осень.ОчиститьЗаготовкиДляАвтоИнициализации();
	ПодключитьСценарий(
		ОбъединитьПути(ТекущийСценарий().Каталог, "../src/Заготовки/Инициализация_autumn-validate.os"),
		"ЛокальнаяЗаготовка"
	);
	Осень.ДобавитьЗаготовкуДляАвтоИнициализации("ЛокальнаяЗаготовка");
КонецПроцедуры

&Тест
Процедура ВалидацияОграничениеВалидноРаботает() Экспорт
	
	// Дано
	Поделка = Новый Поделка();
	Поделка.ЗапуститьПриложение();
	
	Валидатор = Поделка.НайтиЖелудь("Валидатор");
	
	ВложенныйОбъект = Новый ТестовыйОбъект;
	
	ТестовыйОбъект = Новый ТестовыйОбъект;
	ТестовыйОбъект.Поле1 = "Значение1";
	ТестовыйОбъект.ВложенныйОбъект = ВложенныйОбъект;
	
	// Когда
	РезультатВалидации = Валидатор.Валидировать(ТестовыйОбъект);
	
	// Тогда
	Ожидаем.Что(РезультатВалидации).ИмеетДлину(2);
	
	Нарушение = РезультатВалидации[0];
	Ожидаем.Что(Нарушение.Объект()).Равно(ТестовыйОбъект);
	Ожидаем.Что(Нарушение.ИмяСвойства()).Равно("Поле2");
	Ожидаем.Что(Нарушение.ТипСвойства()).Равно("Поле");
	Ожидаем.Что(Нарушение.Сообщение()).Равно("Значение не заполнено");
	Ожидаем.Что(Нарушение.Код()).Равно("filled");
	
	Нарушение = РезультатВалидации[1];
	Ожидаем.Что(Нарушение.Объект()).Равно(ТестовыйОбъект);
	Ожидаем.Что(Нарушение.ИмяСвойства()).Равно("ВложенныйОбъект");
	Ожидаем.Что(Нарушение.ТипСвойства()).Равно("Поле");
	Ожидаем.Что(Нарушение.Сообщение()).Равно("Значение не валидно");
	Ожидаем.Что(Нарушение.Код()).Равно("valid");
	
КонецПроцедуры

&Тест
Процедура АннотацияВалидноНадВозвращаемымЗначениемРаботает() Экспорт
	
	// Дано
	Поделка = Новый Поделка();
	Поделка.ДобавитьЖелудь(Тип("ТестовыйОбъект"));
	Поделка.ЗапуститьПриложение();
	
	ТестовыйОбъект = Поделка.НайтиЖелудь("ТестовыйОбъект");
	ТестовыйОбъект.Поле1 = "Значение1";
	ТестовыйОбъект.Поле3 = -15;
	
	// Когда-тогда
	Ожидаем.Что(ТестовыйОбъект)
		.Метод("ВернутьСебя")
		.ВыбрасываетИсключение("Возвращаемое значение метода ВернутьСебя в желуде ТестовыйОбъект не прошло валидацию");
	
КонецПроцедуры

&Тест
Процедура АннотацияВалидноНадПараметромМетодаРаботает() Экспорт
	
	// Дано
	Поделка = Новый Поделка();
	Поделка.ДобавитьЖелудь(Тип("ТестовыйОбъект"));
	Поделка.ЗапуститьПриложение();
	
	ТестовыйОбъект1 = Поделка.НайтиЖелудь("ТестовыйОбъект");
	ТестовыйОбъект1.Поле1 = "Значение1";
	ТестовыйОбъект1.Поле2 = "Значение2";
	
	ТестовыйОбъект2 = Поделка.НайтиЖелудь("ТестовыйОбъект");
	ТестовыйОбъект2.Поле1 = "Значение1";
	
	ТестовыйОбъект = Поделка.НайтиЖелудь("ТестовыйОбъект");
	
	// Когда-тогда
	ПараметрыВызова = Новый Массив;
	ПараметрыВызова.Добавить(ТестовыйОбъект1);
	ПараметрыВызова.Добавить(ТестовыйОбъект2);
	Ожидаем.Что(ТестовыйОбъект)
		.Метод("ПроверитьВходящиеПараметры", ПараметрыВызова)
		.ВыбрасываетИсключение("Параметр Параметр2 метода ПроверитьВходящиеПараметры в желуде ТестовыйОбъект не прошел валидацию");
	
КонецПроцедуры

// ПередЗапускомТеста();
// АннотацияВалидноНадПараметромМетодаРаботает();
