#Использовать autumn

Перем _Поделка;

Перем СоответствиеВалидацийИОграничений;

Функция ЭтоАннотацияВалидации(ИмяАннотации) Экспорт
	Возврат СоответствиеВалидацийИОграничений.Получить(НРег(ИмяАннотации)) <> Неопределено;
КонецФункции

Функция СоздатьОбъектВалидации(Ограничение) Экспорт
	
	ИмяОграничения = Ограничение.ИмяОграничения();
	ИмяЖелудяВалидации = СоответствиеВалидацийИОграничений.Получить(НРег(ИмяОграничения));
	
	Валидация = _Поделка.НайтиЖелудь(ИмяЖелудяВалидации);
	Валидация.Инициализировать(Ограничение);
	
	Возврат Валидация;
	
КонецФункции

&Желудь("ФабрикаВалидаций")
Процедура ПриСозданииОбъекта(
		&Пластилин Поделка,
		&Пластилин КонтейнерАннотаций
	)
	_Поделка = Поделка;
	
	ОпределенияЖелудейВалидаций = Поделка.ПолучитьОпределенияЖелудей("Валидация");
	ОпределениеАннотацииВалидация = КонтейнерАннотаций.ПолучитьОпределениеАннотации("Валидация");
	
	СоответствиеВалидацийИОграничений = Новый Соответствие;
	
	Для Каждого ОпределениеЖелудя Из ОпределенияЖелудейВалидаций Цикл
		АннотацияВалидация = ОпределениеЖелудя.Завязь().ДанныеМетода().Аннотации.Найти("валидация", "Имя");
		
		ОбъектАннотации = ОпределениеАннотацииВалидация.СоздатьОбъектАннотации(АннотацияВалидация);
		
		ИмяОграничения = ОбъектАннотации.ИмяОграничения();
		
		СоответствиеВалидацийИОграничений.Вставить(НРег(ИмяОграничения), ОпределениеЖелудя.Имя());
	КонецЦикла;
	
КонецПроцедуры
